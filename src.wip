const size = 128;

const arr1: *int = allocate(size * 8);
const arr2: *int = allocate(size * 8);

var i = 0;

while (i - size) {
  *(arr1 + i) = 0;
  ++i;
}

*(arr1 + (size - 1)) = 1;

const processLine = (arr1: *int, arr2: *int, size: int) => {

  var i = 0;
  while (i - size) {
    var left = 0;
    if (i) {
      left = *(arr1 + (i - 1));
    }
    var right = 0;
    if (i - size - 1) {
      right = *(arr1 + (i + 1));
    }

    const current =  *(arr1 + i);

    (*(arr2 + i)) = ((left: int, middle: int, right: int): int => {
      if (left) {
        if (middle) {
          if (right) {
            return 0;
          }
          return 1;
        }
        if (right) {
          return 1;
        }
        return 0;
      }
      if (middle + right) {
        return 1;
      }
      return 0;
    })(left, current, right);
    ++i;
  }
};

print("\nRule 110:\n");

var j = size;
while (j--) {
  const drawLine = (arr: *int, size: int) => {
    var i = 0;
    while (i - size) {
      const current = *(arr + i);

      if (current) {
        print("#");
      }

      if (current - 1) {
        print(" ");
      }
      ++i;
    }
    print("\n");
  };

  drawLine(arr1, size);
  processLine(arr1, arr2, size);
  drawLine(arr2, size);
  processLine(arr2, arr1, size);
}
